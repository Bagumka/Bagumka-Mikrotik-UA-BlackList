# Copyright Â© 2021 Vadym Klymenko <vadim@explorer.od.ua>
# License: https://github.com/Bagumka/Mikrotik-UA-BlackList/blob/main/LICENSE
#
# UA-BLACKLIST v2.2
#

#
# Interface connection name:
#
:local blackholeGw "pptp-bypass";
:local blackholeReconnect false;
# Available modes: "route" and "firewall"
:local blackholeMode "firewall"

#
# URL where to get blocked subnets list
#
:local apiURL "https://uablacklist.net/"
:local apiFilePrefix "subnets_mikrotik_"
:local apiFileSuffix ".txt"

######### Nothing interesting below this line ##############
:local listName "UABL"
:local totalActive 0
:local totalAdded 0
:local totalDeleted 0
:local totalIgnored 0
:local apiPiece 0
:local isEnd false
:local message
:set message "$listName: Starting..."
/log info ($message)
:put $message
:if ($blackholeReconnect) do={
:set message "$listName: Interface reconnection set in config."
/log info ($message)
:put $message
   /interface disable $blackholeGw;
   :delay 1;
   /interface enable $blackholeGw;
   :delay 10;
};
:set message "$listName: UA-BLACKLIST registry API URL set to: $apiURL mode: $blackholeMode"
/log info ($message)
:put $message
:do {
    :local fileName "$apiFilePrefix$apiPiece$apiFileSuffix"
    :local apiPath "$apiURL$fileName"
    :local fetchData
    :local fetchResult ""
    :local fetchStatus ""
    :local content ""
    :local contentLen 0
    :local newAddressList [:toarray ""]
    :local newAddressListCount 0
    :local addressesActive 0
    :local addressesAdded 0
    :local addressesDeleted 0
    :local addressesIgnored 0
    :local lineEnd 0
    :local line ""
    :local lastEnd 0
    :local company ""
    #
    # Fetching file
    #
    :do {
        :set message "$listName: trying to fetch $apiPath"
        /log debug ($message)
        :put $message
        :set fetchData [/tool fetch mode=https output=user url=$apiPath as-value]
        :set fetchResult [:toarray $fetchData]
        :set content ($fetchResult->"data")
        :set contentLen [:len $content]
        :set fetchStatus ($fetchResult->"status")
        if ($fetchStatus != "finished") do={
            :set message "$listName: API connection error." 
            /log error ($message)
            :put $message
            :set isEnd true
        } else={
            :set message "$listName: $apiPath successfully downloaded" 
            /log debug ($message)
            :put $message
        }
    } on-error={
        if ($apiPiece = 0) do={
            :set message "$listName: API connection error." 
            /log error ($message)
            :put $message
            :set isEnd true
        } else={
            :set message "$listName: No more pieces to fetch. Finished." 
            /log info ($message)
            :put $message
            :set isEnd true
        }
    }
    if (!$isEnd) do={
        #
        # Getting array of new addresses from downloaded file
        #
        :set message "$listName: Importing new addresss list..."
        /log debug ($message)
        :put $message
        :while ($lastEnd < $contentLen) do {
            :set lineEnd [:find $content "\n" $lastEnd]
            :set line [:pick $content $lastEnd $lineEnd]
            :set lastEnd ($lineEnd+1)
            :local entry [:pick $line 0 ($lineEnd-1)]
            :if ([:pick $line 0 1] != "#") do={
                :if ([:len $entry] > 0) do={
                    set newAddressList ($newAddressList, $line) 
                    set newAddressListCount ($activeAddressCount +1)
                }
            } else={
                :set company [:pick $line 2 ($lineEnd)]
            }
        }
        #
        # Getting array of active rules
        #
        :set message "$listName: Importing active address list..."
        /log debug ($message)
        :put $message
        :local activeAddressList [:toarray ""]
        :local activeAddressCount 0
        if ($blackholeMode = "route") do={
            :foreach activeAddressItem in=[/ip route find comment="$listName: $apiPiece $company"] do={
                set activeAddressList ($activeAddressList, [/ip route get $activeAddressItem dst-address]) 
                set activeAddressCount ($activeAddressCount +1)
            }
        }
        if ($blackholeMode = "firewall") do={
            :foreach activeAddressItem in=[/ip firewall address-list find list=$listName comment~"$apiPiece $company"] do={
                set activeAddressList ($activeAddressList, [/ip firewall address-list get $activeAddressItem address]) 
                set activeAddressCount ($activeAddressCount +1)
            }
        }
        
        # 
        # God of debugging need more debugging!
        #
        # :set message "$activeAddressList"
        # /log debug ($message)
        # :put $message
        # :set message "$newAddressList"
        # /log debug ($message)
        # :put $message

        #
        # Deleting outdated rules
        #
        :foreach arli in=$activeAddressList do {
            set addressesActive ($addressesActive +1)
            :local exist false
            :foreach nrli in=$newAddressList do={
                if ([find $arli "/"] <7) do={
                     :set $arli ("$arli/32")
                }
                if ($arli = $nrli) do={
                    set exist true
                }
            }
            if (!$exist) do={
                :set message "$listName: Outdated rule for $arli. Removing"
                /log debug ($message)
                :put $message
                if ($blackholeMode = "route") do={
                    /ip route remove [/ip route find dst-address=$arli comment="$listName $apiPiece $company"]
                } 
                if ($blackholeMode = "firewall") do={
                    :local hostMaskLocation [find $arli "/32"]
                    if ( $hostMaskLocation >=7 ) do={
                        :set $arli [:pick $arli 0 $hostMaskLocation] 
                    }
                    /ip firewall address-list remove [/ip firewall address-list find list=$listName address=$arli comment="$apiPiece $company"]
                }
                :set addressesDeleted ($addressesDeleted + 1)
            }
        }
        #
        # Adding new rules
        #
        :foreach nrli in=$newAddressList do {
            :local exist false
            :foreach arli in=$activeAddressList do={
                if ([find $arli "/"] <7) do={
                     :set $arli ("$arli/32")
                }
                if ($arli = $nrli) do={
                    set exist true
                }
            }
            if (!$exist) do={
                :local existGlobal false
                if ($blackholeMode = "route") do={
                    :foreach activeAddressItem in=[/ip route find comment~"$listName"] do={
                        if ([/ip route get $activeAddressItem dst-address] = $nrli) do={
                             set existGlobal true
                        }
                    }
                }
                if ($blackholeMode = "firewall") do={
                    :foreach activeAddressItem in=[/ip firewall address-list find list="$listName"] do={
                        :local ip [/ip firewall address-list get $activeAddressItem address]
                        if ([find $ip "/"] <7) do={
                            :set $ip ("$ip/32")
                        }
                        if ($ip = $nrli) do={
                             set existGlobal true
                        }
                    }
                }    
                if (!$existGlobal) do={
                    :set message "$listName: New rule found for $nrli"
                    :put $message
                    /log warning ($message)
                    if ($blackholeMode = "route") do={
                        /ip route add dst-address=$nrli gateway=$blackholeGw comment="$listName: $apiPiece $company" check-gateway=ping
                    }
                    if ($blackholeMode = "firewall") do={
                        /ip firewall address-list add list=$listName address=$nrli comment="$apiPiece $company"         
                    }
                    :set addressesAdded ($addressesAdded + 1)
                } else={
                    :set addressesIgnored ($addressesIgnored + 1)
                    :set message "$listName: Rule allready exist for $nrli. Ignored"
                    :put $message
                    /log warning ($message)
                }
            }
        }
        :set totalActive ($totalActive + $addressesActive)
        :set totalDeleted ($totalDeleted + $addressesDeleted)
        :set totalAdded ($totalAdded + $addressesAdded)
        :set totalIgnored ($totalIgnored + $addressesIgnored)
        :set message "$listName: Piece $apiPiece: $addressesActive active, $addressesDeleted deleted, $addressesAdded added, $addressesIgnored ignored."
        /log debug ($message)
        :put $message
    }
    :set apiPiece ($apiPiece+1)
} while (!$isEnd)
:local processed (apiPiece)
:set message "$listName: Finished. $processed files processed: $totalActive active, $totalDeleted deleted, $totalAdded added, $totalIgnored ignored."
/log info ($message)
:put $message
